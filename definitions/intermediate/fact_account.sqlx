config { tags: ["hash-process"],
         type: "incremental",
          bigquery: {
            partitionBy: "date",
            clusterBy: ["account_number"]
          },
         description: "This is the fact table with balance for each account by date",
         columns:{
            date: "Date",
            account_number: "Account Number",
            withdrawal_amount: "Withdrawal Amount",
            deposit_amount: "Deposit Amount",
            balance: "Balance",
        },
        schema: `${dataform.projectConfig.vars.dataset_id_curated}`,
        database: `${dataform.projectConfig.vars.dataset_projectid_curated}`,
        name: "fact_account"
      }
SELECT
  PARSE_DATE('%Y-%m-%d', date) AS date,
  account_number,
  SUM(withdrawal_amount) withdrawal_amount,
  SUM(deposit_amount) deposit_amount,
  SUM(balance) balance
FROM
   `${dataform.projectConfig.vars.dataset_projectid_landing}.${dataform.projectConfig.vars.dataset_id_landing}`.raw_transactions
${when(incremental(), `WHERE date = ${dataform.projectConfig.vars.start_date}`, `WHERE 1 = 1`)}
AND successful_operation = true
GROUP BY ALL
